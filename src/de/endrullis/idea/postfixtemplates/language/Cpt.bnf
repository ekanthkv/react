{
  parserClass="de.endrullis.idea.postfixtemplates.language.parser.CptParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Cpt"
  psiImplClassSuffix="Impl"
  psiPackage="de.endrullis.idea.postfixtemplates.language.psi"
  psiImplPackage="de.endrullis.idea.postfixtemplates.language.psi.impl"

  elementTypeHolderClass="de.endrullis.idea.postfixtemplates.language.psi.CptTypes"
  elementTypeClass="de.endrullis.idea.postfixtemplates.language.psi.CptElementType"
  tokenTypeClass="de.endrullis.idea.postfixtemplates.language.psi.CptTokenType"

  psiImplUtilClass="de.endrullis.idea.postfixtemplates.language.psi.impl.CptPsiImplUtil"
}

CptFile ::= (template|COMMENT)*

template ::= (TEMPLATE_NAME SEPARATOR TEMPLATE_DESCRIPTION) mappings {
    //pin=3
    recoverWhile="recover_parser"
    mixin="de.endrullis.idea.postfixtemplates.language.psi.impl.CptNamedElementImpl"
    implements="de.endrullis.idea.postfixtemplates.language.psi.CptNamedElement"
    methods=[getTemplateName getTemplateDescription getName setName getNameIdentifier getPresentation]
}

mappings ::= mapping*

mapping ::= (CLASS_NAME (BRACKET_OPEN CLASS_NAME BRACKET_CLOSE)? MAP replacement) {
    //pin=3
    recoverWhile="recover_parser"
    mixin="de.endrullis.idea.postfixtemplates.language.psi.impl.CptNamedElementImpl"
    implements="de.endrullis.idea.postfixtemplates.language.psi.CptNamedElement"
    methods=[getMatchingClassName getConditionClassName getReplacementString getName setName getNameIdentifier getPresentation]
}

replacement ::= (templateCodeG|escape|templateVariable)+

templateCodeG ::= TEMPLATE_CODE

escape ::= TEMPLATE_ESCAPE

templateVariable ::= TEMPLATE_VARIABLE_START templateVariableNameG
										(TEMPLATE_VARIABLE_SEPARATOR templateVariableExpressionG
										(TEMPLATE_VARIABLE_SEPARATOR templateVariableValueG)?)? TEMPLATE_VARIABLE_END

templateVariableNameG ::= TEMPLATE_VARIABLE_NAME
templateVariableExpressionG ::= TEMPLATE_VARIABLE_EXPRESSION*
templateVariableValueG ::= TEMPLATE_VARIABLE_VALUE+

private recover_parser ::= !(TEMPLATE_NAME|CLASS_NAME|COMMENT)
